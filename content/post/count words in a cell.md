---
title: Count words in a excel cell 
date: 2018-08-01
categories: [Other]
tags: [office]
draft: false
version: 1.0
emoji: true
typora-root-url: ../
---



엑셀의 셀에서 단어의 수만 셀 수 있는 공식이 필요하게 되었습니다. VBA를 이용하면 그럭저럭 되긴 하겠지만 문서를 열 때마다 보안 경고를 띄우는 불편함으로... 가급적 쓰고 싶지 않았기 때문에 함수 - 영어로는 formula로 표현되는 - 를 사용하는 방법을 찾아서 정리해 봤습니다. 

<img src="/post/cwiac_img/cwiac.jpg" style="zoom:70%" />

<!--more--> 

다행히도 "counting word in a cell" 정도의 키워드로 기본 공식이 아래와 같은 공식이 검색되긴 했지만 몇가지 문제가 있었습니다. 

```vbscript
=IF(ISBLANK(A1),0,LEN(TRIM(A1))-LEN(SUBSTITUTE(A1," ",""))+1)
```

- 문장내에 공백 (whitespace)는 몇개가 있어도 잘 동작하지만
- 셀 내용에 줄바꿈이 들어가는 경우에도 단어수 계산이 잘못된다
- 쉼표와 같은 구분자가 단어와 떨어져 있는 경우에는 단어수 계산이 잘못된다



결국 공식을 써서 단어수를 자동으로 셀 수는 있지만 "세심하게 입력을 하지 않으면 오류가 날 수도 있는" 어정쩡한 상황...

구글링에서 얻은 기본 공식을 기반으로 위에 열거한 문제점들을 어느 정도 개선한 공식을 공유하면서 겸사겸사 어떻게 작동을 하는지도 설명을 해보고자 합니다. 



#### 어떤 공식을 쓰면 되는 건가요?

시간이 없으신 분들을 위해 아래의 가정하에 만든 공식을 먼저 공유합니다.  

- 셀 내용에 줄바꿈이 들어가는 경우에도 문제가 없고 
- 단어 구분자가 단어와 떨어져 있는 경우에도 문제가 없지만
- 구분자는 **쉼표**만 사용할 수 있는 **제약**을 가집니다 (구분자는 다른 걸로 바꿀 수는 있습니다)

아래처럼 말이죠. 

```vbscript
=IF(LEN(TRIM(A1))=0,0,LEN(TRIM(SUBSTITUTE(SUBSTITUTE(A1,CHAR(10)," "),",", " "))) - LEN(SUBSTITUTE(TRIM(SUBSTITUTE(SUBSTITUTE(A1,CHAR(10)," "),","," "))," ","")) + 1)
```

처음 기본 공식에 비해 상당히 많이~~ 길어졌습니다. 직접 입력하기보다는 복사&붙이기가 정신 건강에 이롭습니다 :smile:

> 위의 공식에서 "A1"으로 쓰여져 있는 부분이 셀 명이니 실제 적용할 문서에 맞게 바꿔 쓰세요 
>



혹시나 구분자로 **","**가 아닌 다른걸 사용해야 하는 분들을 위해 - 위의 공식에는 ","가 너무 많아서 바꾸려면 많이 헷갈린 관계로 -  **"$"**를 구분자로 넣는 예도 보여드리니 잘 보시고 "$" 부분을 필요한 구분자로 바꿔서 사용하시기 바랍니다. 

```vbscript
=IF(LEN(TRIM(A1))=0,0,LEN(TRIM(SUBSTITUTE(SUBSTITUTE(A1,CHAR(10)," "),"$", " "))) - LEN(SUBSTITUTE(TRIM(SUBSTITUTE(SUBSTITUTE(A1,CHAR(10)," "),"$"," "))," ","")) + 1)
```



#### 공식을 한번 분해해 볼까요? 

위의 공식을 보고 저처럼 이해가 안되는 것은 쓰기가 꺼려지시는 분들이나 도전 정신이 발동하시는 분 또는 좀 더 활용을 원하시는 분들을 위한 코너!

공식이 길어 보이고 뭔가 복잡한 것 같지만 쪼개보면 괄호가 많아서 읽기가 힘들뿐이지 그리 어려운 것은 아닙니다.



위에 제시한 공식을 설명하기 위해 사람이 입력하면 흔히 만들 수 있는 입력 패턴들이 포함된 엑셀의 셀 입력 예를 보겠습니다. 아래 그림에서 좌측이 사람이 보는 셀이고, 우측은 엑셀이 내부적으로 사용하는 - 엑셀은 텍스트라고 표현하는 - 문장 입니다. 

<img src="/post/cwiac_img\example.jpg" style="zoom:70%" />

- 위 그림에서 **"˽"**  기호는 공백 (whitespace)을 가시화한 것으로 스페이스키를 한번 누른것에 해당합니다.  
- 위 그림에서 **"&#8629;"** 기호는 줄바꿈을 가시화한 것으로  엑셀 셀내에서 줄바꿈을 할 때 쓰는 "ALT+ENTER"에 해당합니다.  



#### 1st) 줄바꿈 문자 없애기

처음 할 일은 줄바꿈 문자인 "&#8629;" 을 없애는 것입니다. 영어로 표현하면 "**substitute** carriage return to something another" 정도로 표현할 수 있겠고 이 문서에서는 이해를 돕기 위해 공백으로 바꾸겠습니다만 삭제해 버려도 무방합니다.  

사용하는 함수는 **SUBSTITUTE**이고 줄바꿈 문자인 **"&#8629;"** 는 엑셀에서 **"CHAR(10)"** 으로 표현하므로 아래와 같이 사용하면 "A1" 셀에 있는 모든 **줄바꿈** 문자를 **공백**으로 바꿔줍니다. 

<img src="/post/cwiac_img\substitute_cr.jpg" style="zoom:70%" />



#### 2nd) 구분자 없애기 

그 다음은 "첫번째"의 결과를 대상으로 구분자에 해당하는 ","를 공백으로 바꾸는 것입니다.  

사용하는 함수는 첫번째와 동일한 SUBSTITUTE이며 대상을 "CHAR(10)" 대신에 **","**로 바꾸서 첫번째의 결과값에 적용하면 됩니다 

<img src="/post/cwiac_img\substitute_sep.jpg" style="zoom:70%" />



#### 3rd) 여분의 공백을 정리하기  

자... 이제 셀의 내용은 단어와 공백만 남았고 단어 사이의 공백이 여러개라는 문제만 해결하면 문장이 깔끔해 질 겁니다. 

이럴 때 사용하는 함수가 **"TRIM"**입니다. TRIM 함수는 문장의 제일 앞과 뒤에 보이지 않게 존재하는 공백을 모두 제거할 뿐만 아니라 **단어와 단어 사이에 있는 공백이 여러개라면 이것도 1개만 남기고 모두 제거**합니다. 아래처럼 말이죠. 

<img src="/post/cwiac_img\trim.jpg" style="zoom:70%" />

세번째 공식까지 적용한 결과인 위 그림의 오른쪽 문장을 보면 **"단어"**와 **"단어들을 구분하는 1개의 공백"**만 남았습니다. 

이 문장을 기준으로 단어의 수를 알아내는 것은 아래와 같은 공식으로 얻을 수 있습니다. 

```
단어의 수 = 공백으로 구분된 문장의 모든 글자의 수 - 문장에서 공백을 제외한 글자의 수 + 1 = 11 - 8 + 1 = 4
```

<img src="/post/cwiac_img\fomula.jpg" style="zoom:70%" />

자 그럼 위의 공식을 완성하기 위한 마지막 두 단계를 진행하죠. 



#### 4th) 공백을 전혀 없는 문장 만들기  

세번째 결과에 또 **"SUBSTITUTE"**를 적용합니다. 대신 이번에는 정말로 **"공백을 없애기 위한 것"**이므로 바꿀 문자는 빈 문자 `""`가 되겠습니다. 

<img src="/post/cwiac_img/substitute_sp.jpg" style="zoom:70%" />



#### 5th) 단어수를 구합시다

자, 이제 단어수를 알아내는데 필요한 **"단어 사이에 하나의 공백만 있는 문장"**은 "3rd)" 에서 구했고 **"단어 사이의 공백을 모두 제거한 문장"**은 "4th)" 에서 구했으니 문장의 길이를 구해 보도록 하겠습니다.  

네번째 결과에 엑셀에서 단어수를 구하는 함수인 **"LEN"**을 적용하면 **"단어 사이의 공백을 모두 제거한 문장의 글자 수"**를 구할 수 있습니다. 

<img src="/post/cwiac_img/len.jpg" style="zoom:70%" />



**"단어 사이에 하나의 공백만 있는 문장의 글자수"**는 어떻게 구할 수 있을까요?

엑셀이 공식에서 변수를 지원했다면 위의 세번째 과정의 결과를 가지고 "LEN" 함수로 미리 글자수를 세서 저장을 해 둔 후 다섯번째에서 얻은 글자수를 빼면 되었겠지만... 엑셀은 공식내에서 변수를 지원하지 않기 때문에 무식하게 보일지 모르겠지만 "1st)" ~ "3rd)" 과정을 한번 더 한 후 "LEN"를 적용하면 됩니다. :sweat_smile:   아래처럼 말이죠 

<img src="/post/cwiac_img/len_3.jpg" style="zoom:70%" />

이 두가지 연산을 같이 사용해서 단어수를 계산하는 공식은 아래처럼 만들어집니다. 

```vbscript
= LEN(TRIM(SUBSTITUTE(SUBSTITUTE(A1,CHAR(10)," "),",", " "))) - LEN(SUBSTITUTE(TRIM(SUBSTITUTE(SUBSTITUTE(A1,CHAR(10)," "),","," "))," ",""))
```



#### last) 예외 처리 

다 된 것 같지만 아직 한가지 경우가 더 남아 있습니다. 바로 **"아무것도 입력하지 않은 경우"**에 대한 예외 처리입니다.  

아무것도 입력하지 않은 셀을 대상으로 위의 단어수 세기 공식을 적용하면 아래처럼 1개의 단어가 있는 걸로 계산됩니다. 

```
단어의 수 = 문장의 모든 글자의 수 - 문장에서 공백을 제외한 글자의 수 + 1 = 0 - 0 + 1 = 1
```

이런 예외 처리에 자주 사용되는 게 조건문 함수인 **"IF"**입니다. 아무것도 입력하지 않거나 공백만 입력한 상태인 `"LEN( TRIM(A1) ) = 0"` 라면 0을 사용하도록 하면 되고, 그 외의 경우에는 위 공식의 결과값을 사용하도록 하면 됩니다. 

<img src="/post/cwiac_img/if.jpg" style="zoom:70%" />

위 그림의 "our formula" 부분에 다섯번째 과정의 공식을 넣으면 드디어 모든 과정이 마무리되고 처음에 보여드린 긴~~  공식이 만들어집니다.

이제 여러분이 할 일은 데이타를 입력할 때 "구분자"는 ","와 공백(" ")만 쓰고 다른것은 쓰지 않도록 주의하는 일만 남았습니다. 아니면 좀 더 길어지겠지만 SUBSTITUTE를 추가해서 여러개의 구분자를 추가하는 공식을 만들수도 있겠지요. 

----

*Please, comments if there is anything wrong or need to update.*   

*Written by [zsdotkr@gmail.com](mailto:zsdotkr@gmail.com) with CC license : [CC BY-NC-ND 4.0](http://creativecommons.org/licenses/by-nc-nd/4.0/)* 